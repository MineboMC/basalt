plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.1.20-Beta1'
    id 'org.jetbrains.kotlin.kapt' version '2.1.20-Beta1'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'maven-publish'
}

allprojects {
    group = 'net.minebo.basalt'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.enonic.com/public/' }
        maven { url "https://oss.sonatype.org/content/groups/public/" }
        maven { url "https://repo.aikar.co/content/groups/aikar/" }
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
        maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url "https://repo.glaremasters.me/repository/public/" }
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "maven-publish"

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"

        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.22'
        kapt 'org.projectlombok:lombok:1.18.22'

        // Spigot
        implementation 'co.aikar:acf-paper:0.5.1-20240125.131356-33'
        implementation 'net.kyori:adventure-platform-bukkit:4.3.0'
        implementation 'club.minnced:discord-webhooks:0.8.2'
        implementation 'com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.4'

        // Discord
        implementation 'net.dv8tion:JDA:5.0.0-beta.20'
        implementation 'ltd.matrixstudios:amber:1.1.0'
        implementation 'com.github.Carleslc.Simple-YAML:Simple-Yaml:1.8.4'

        // Apache
        implementation 'org.apache.maven:maven-artifact:3.0.3'

        // Honey & Papi
        implementation fileTree(dir: '../libraries', include: '*.jar')
    }

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(11))
    }

    tasks.named('compileKotlin') {
        kotlinOptions.javaParameters = true
        kotlinOptions.jvmTarget = "11"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

tasks.named('shadowJar') {
    archiveClassifier.set('') // Remove the 'all' classifier
    mergeServiceFiles()

    // Include outputs from all subprojects
    subprojects.each { subproject ->
        from(subproject.sourceSets.main.output)
    }
}

artifacts {
    archives tasks.named('shadowJar').get()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact tasks.named('shadowJar').get()
        }
    }
}
